plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'ghrobotics.org'
version '1.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "net.dv8tion:JDA:4.1.1_108"
    implementation 'com.google.api-client:google-api-client:1.30.7'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.30.5'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev581-1.25.0'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

    implementation("com.fasterxml.jackson.core:jackson-databind") {
        version {
            strictly "2.10.1"
        }
        because "It breaks otherwise"
    }
}

task fatJar(type: Jar) {
    archiveClassifier = 'fat'

    manifest {
        attributes 'Main-Class': 'AppKt'
        version = null
    }

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}